-- Wide Mouthed Frog Protocol

#Free variables

A, B : Agent
S : Server
kab : SessionKey
ts, ts' : TimeStamp
SKey : Agent -> ServerKey
InverseKeys = (SKey,SKey)

#Processes

INITIATOR(A,S,kab) knows SKey(A)
RESPONDER(B) knows SKey(B)
SERVER(S) knows SKey

#Protocol description

0.    -> A : B
1.  A -> S : {B, ts, kab}{SKey(A)}
[ts==now or ts+1==now]
2.  S -> B : {ts', A, kab}{SKey(B)}
[ts'==now or ts'+1==now]
3.  B -> A : A

#Specification

-- PASS
TimedAgreement(A,B,2,[kab])
-- PASS
StrongSecret(A,kab,[B,S])
-- PASS
Secret(B,kab,[A])

#Actual variables

Alice, Bob, Mallory : Agent
Sam : Server
Kab : SessionKey
TimeStamp = 0 .. 2
MaxRunTime = 1

#Functions

symbolic SKey

#System

INITIATOR(Alice, Sam, Kab)
RESPONDER(Bob)
SERVER(Sam); SERVER(Sam); SERVER(Sam)

#Intruder Information

Intruder = Mallory
IntruderKnowledge = {Alice, Bob, Mallory, Sam, SKey(Mallory)}
