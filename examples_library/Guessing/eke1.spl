-- Not EKE

#Free variables

a, b : Agent
na, nb : Nonce
k : SessionKey
pk : PubKey
passwd : Agent x Agent -> Password
InverseKeys = (passwd, passwd), (pk, pk), (k,k)

#Processes

INITIATOR(a, pk, na) knows passwd(a,b)
RESPONDER(b, nb, k) knows passwd(a,b)

#Protocol description

0.    -> a : b
-- [ a != b ]
1.  a -> b : a, {pk}{passwd(a,b)}
-- [ a != b ]
2.  b -> a : { {k}{pk} }{passwd(a,b)}
3.  a -> b : {na}{k}
4.  b -> a : {na, nb}{k}
5.  a -> b : {nb}{k}

#Specification

-- FAIL
Secret(a, passwd(a,b), [b])
-- FAIL
Secret(b, passwd(a,b), [a])
-- FAIL
Agreement(b,a,[na,nb])
-- FAIL
Agreement(a,b,[na,nb])

#Actual variables

A, B, I : Agent
Na, Nb, Ni : Nonce
K, Ki : SessionKey
PK, PKi : PubKey
InverseKeys = (PK, PK), (PKi, PKi), (K, K), (Ki, Ki)

#Functions

symbolic passwd

#System

INITIATOR(A, PK, Na)
RESPONDER(B, Nb, K)

#Intruder Information

Intruder = I
IntruderKnowledge = {A, B, I, Ni, PKi,  \
                     passwd(I, A), passwd(I, B), passwd(A, I), passwd(B, I)}

-- FAIL
Guessable = Password
